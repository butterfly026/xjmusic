//  Copyright (c) XJ Music Inc. (https://xj.io) All Rights Reserved.

import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
  id 'application'
  id 'com.google.cloud.tools.jib'
  id 'de.undercouch.download'
  id 'distribution'
  id 'jacoco'
  id 'java'
  id 'java-test-fixtures'
  id 'org.ajoberstar.grgit'
  id 'org.flywaydb.flyway'
  id 'org.openapi.generator'
  id 'org.sonarqube'
}

group = 'io.xj'
version = '1.0'

description = 'XJ Music platform backend services'

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

jacocoTestReport {
  reports {
    xml.enabled true
  }
}

apply plugin: 'java'

repositories {
  mavenLocal()
  maven {
    url = 'https://repo.maven.apache.org/maven2'
  }
  maven {
    url = 'https://packages.confluent.io/maven/'
  }
}

dependencies {
  implementation project(":hub")
  implementation project(":lib")
  testImplementation(testFixtures(project(":hub")))
  testFixturesImplementation(project(":lib"))
  testFixturesImplementation(project(":hub"))
  testFixturesImplementation(testFixtures(project(":hub")))
  //
  implementation 'ch.qos.logback:logback-classic:1.2.3'
  implementation 'com.amazonaws:aws-java-sdk-secretsmanager:1.12.7'
  implementation 'com.amazonaws:aws-java-sdk:1.11.119'
  implementation 'com.fasterxml.jackson.core:jackson-annotations:2.10.1'
  implementation 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.10.2.1'
  implementation 'com.fasterxml.jackson.module:jackson-module-guice:2.10.3'
  implementation 'com.github.axet:TarsosDSP:2.4-1'
  implementation 'com.github.rholder:snowball-stemmer:1.3.0.581.1'
  implementation 'com.google.api-client:google-api-client:1.30.9'
  implementation 'com.google.apis:google-api-services-plus:v1-rev590-1.25.0'
  implementation 'com.google.guava:guava:28.2-jre'
  implementation 'com.google.inject.extensions:guice-assistedinject:5.0.1'
  implementation 'com.google.inject:guice:5.0.1'
  implementation 'com.typesafe:config:1.4.0'
  implementation 'com.zaxxer:HikariCP:3.4.1'
  implementation 'commons-codec:commons-codec:1.10'
  implementation 'commons-io:commons-io:2.6'
  implementation 'javax.activation:activation:1.1'
  implementation 'javax.annotation:javax.annotation-api:1.3.1'
  implementation 'javax.cache:cache-api:1.1.1'
  implementation 'javax.validation:validation-api:2.0.1.Final'
  implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
  implementation 'me.xdrop:fuzzywuzzy:1.1.9'
  implementation 'net.logstash.logback:logstash-logback-encoder:6.6'
  implementation 'org.apache.httpcomponents:httpclient:4.5.8'
  implementation 'org.eclipse.jetty:jetty-server:9.4.26.v20200117'
  implementation 'org.eclipse.jetty:jetty-servlet:9.4.26.v20200117'
  implementation 'org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.33'
  implementation 'org.glassfish.jersey.core:jersey-server:2.33'
  implementation 'org.glassfish.jersey.inject:jersey-hk2:2.33'
  implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:2.33'
  implementation 'org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-simple:2.33'
  implementation 'org.jooq:jooq:3.12.3'
  implementation 'org.json:json:20160810'
  implementation 'org.reflections:reflections:0.9.11'
  implementation 'org.slf4j:log4j-over-slf4j:1.7.13'
  implementation 'org.slf4j:slf4j-api:1.7.5'
  implementation 'org.yaml:snakeyaml:1.18'
  implementation 'software.amazon.awssdk:cloudwatch:2.17.20'
  testFixturesImplementation 'com.google.guava:guava:28.2-jre'
  testFixturesImplementation 'com.google.inject.extensions:guice-assistedinject:5.0.1'
  testFixturesImplementation 'com.google.inject:guice:5.0.1'
  testFixturesImplementation 'com.typesafe:config:1.4.0'
  testFixturesImplementation 'org.jooq:jooq:3.12.3'
  testFixturesImplementation 'org.slf4j:slf4j-api:1.7.30'
  testFixturesImplementation 'redis.clients:jedis:2.9.0'
  testImplementation 'com.github.tomakehurst:wiremock-jre8:2.26.3'
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.assertj:assertj-core:3.8.0'
  testImplementation 'org.eclipse.jetty:jetty-client:9.4.26.v20200117'
  testImplementation 'org.hamcrest:hamcrest-library:1.3'
  testImplementation 'org.mockito:mockito-core:3.2.4'
}

jar {
  manifest {
    attributes 'Main-Class': 'io.xj.nexus.Main'
  }
}

applicationDefaultJvmArgs = [
  "-Xmx3500m"
]

application {
  mainClassName = 'io.xj.nexus.Main'
}

group = 'io.xj'
description = 'nexus'
version = '1.0'

java {
  sourceCompatibility = JavaVersion.VERSION_14
  targetCompatibility = JavaVersion.VERSION_14
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

jacocoTestReport {
  reports {
    xml.enabled true
  }
}

static def gitBranch() {
  def branch = ""
  def proc = "git rev-parse --abbrev-ref HEAD".execute()
  proc.in.eachLine { line -> branch = line }
  proc.err.eachLine { line -> println line }
  proc.waitFor()
  branch
}

jib {
  to.image = gitBranch() == 'main' ? '027141088039.dkr.ecr.us-east-1.amazonaws.com/xj/nexus' : '027141088039.dkr.ecr.us-east-1.amazonaws.com/xj/dev/nexus'
  from.image = '027141088039.dkr.ecr.us-east-1.amazonaws.com/xj/base:main'
  container.jvmFlags = ["-Djava.library.path=/usr/lib/x86_64-linux-gnu"]
}

task generateNexusModels(type: GenerateTask) {
  generatorName = "jaxrs-spec"
  inputSpec = "${projectDir.absolutePath}/spec.yaml".toString()
  outputDir = "${buildDir.absolutePath}/openapi-codegen".toString()
  apiPackage = "io.xj.api.chain"
  invokerPackage = "io.xj.api"
  modelPackage = "io.xj.api"
  configOptions = [
    validateSpec         : "false",
    dateLibrary          : "java14",
    useGenericResponse   : "true",
    openApiNullable      : "true",
    useSwaggerAnnotations: "false"
  ]
}

sourceSets {
  main {
    java {
      srcDir "${buildDir.absolutePath}/openapi-codegen/src/gen/java"
    }
    resources {
      srcDir "${buildDir.absolutePath}/openapi-codegen/src/gen/resources"
    }
  }
}

// Compilation depends on generating all specs
compileJava.dependsOn tasks.generateNexusModels
